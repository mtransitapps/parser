plugins {
    id("java-library")
    alias(libs.plugins.kotlin.jvm)
    alias(libs.plugins.kotlinx.serialization)
    alias(libs.plugins.dependency.guard)
}

def envCI = System.getenv("CI")
def isCI = envCI != null && envCI != ""

java {
    sourceCompatibility = rootProject.javaVersion
    targetCompatibility = rootProject.javaVersion
    toolchain.languageVersion = JavaLanguageVersion.of(rootProject.javaToolchainVersion.toString())
}
kotlin {
    compilerOptions {
        jvmTarget = rootProject.javaVersion
        allWarningsAsErrors = true
    }
}

dependencies {
    api(project(":commons-java"))

    implementation(platform(libs.kotlin.bom))
    implementation(platform(libs.kotlinx.coroutines.bom))
    implementation(platform(libs.kotlinx.serialization.bom))
    implementation(libs.bundles.kotlin)

    implementation(libs.androidx.collection)

    api(libs.bundles.commons)
    api(libs.xerial.SQLiteJDBC)

    testImplementation(libs.bundles.test.unit)
}

// update baseline with: ./gradlew :parser:dependencyGuardBaseline
dependencyGuard {
    // lib
    configuration("compileClasspath")
    configuration("runtimeClasspath")
    // tests
    configuration("testCompileClasspath")
    configuration("testRuntimeClasspath")
}

test {
    useJUnit()
    testLogging {
        if (isCI) {
            events("started", "passed", "skipped", "failed", "standard_out", "standard_error")
        } else {
            events("failed")
        }
    }
}
